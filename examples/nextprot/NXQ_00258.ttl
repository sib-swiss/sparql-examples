prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<https://schema.org/>
ex:NXQ_00258
schema:target <https://sparql.nextprot.org/sparql> ;
 sh:prefixes _:sparql_examples_prefixes ;
  a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
  rdfs:comment '''Proteins involved in diseases due to intronic variants with one selected publication''' ;
 schema:keyword "variant";
 schema:keyword "disease";
 schema:keyword "federated query";
 schema:keyword "tutorial";
 schema:keyword "publication";
  sh:select '''select distinct ?entry (str(?gen) as ?gene) ?diseaseTitle ?sampleArticle where {
 service <http://rdf.disgenet.org/sparql/> {
 select distinct ?protein ?diseaseTitle (sample(?article) as ?sampleArticle) where {
 ?vda sio:SIO_000628 ?variant,?disease .
	?vda sio:SIO_000772 ?article .
 ?disease a sio:SIO_010299 ; dcterms:title ?diseaseTitle . # true disease, use ncit:C7057 for traits
 ?variant a so:0001627 ; dcterms:title ?variantTitle . # intron variant
 ?variant so:associated_with ?gene .
 ?gene a ncit:C16612; sio:SIO_010078 ?protein .
 } group by ?protein ?diseaseTitle
 }
 BIND(IRI(replace(str(?protein),"purl","www")) AS ?unipage) .
 ?entry :swissprotPage ?unipage .
 ?entry :gene / :recommendedName / rdfs:label ?gen .
} order by ?entry'''
.
