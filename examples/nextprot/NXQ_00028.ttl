prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<http://schema.org/>
ex:NXQ_00028
  a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
  sh:prefixes _:sparql_examples_prefixes, _:nextprot_sparql_examples_prefixes ;
  rdfs:comment '''Proteins associated with a disease but without a disease-causing amino-acid substitution variant''' ;
 schema:keyword "variant";
 schema:keyword "disease";
 schema:keyword "tutorial";
 schema:keyword "keyword";
  sh:select '''select distinct ?entry where {
 ?entry :isoform ?iso.
 { 
	 ?iso :medical / rdf:type :Disease.
 } union { 
 ?iso :uniprotKeyword / :term ?kw .
	 ?kw :termType \"Disease\"^^xsd:string
	 filter (?kw != nextprot_cv:KW-0656)
 }
 filter ( not exists { ?entry :isoform / :variant / :disease ?_ . } )
 filter ( not exists { 
	?entry :isoform / :proteoform ?pf.
	?pf :difference / :evidence / :negative false. # positive evidence only
	?pf :diseaseRelatedVariant / :term nextprot_cv:ME_0000013 . # cause of disease
 } ) 
}'''
.
