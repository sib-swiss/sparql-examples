prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<https://schema.org/>
ex:NXQ_00254
schema:target <https://sparql.nexprot.org/sparql> ;
 sh:prefixes _:sparql_examples_prefixes ;
  a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
  rdfs:comment '''Proteins with associated pathways in WikiPathways''' ;
 schema:keyword "pathway";
 schema:keyword "federated query";
 schema:keyword "tutorial";
  sh:select '''select distinct ?entry ?gen ?pathwayname where {
 service <http://sparql.wikipathways.org/sparql> {

 {?geneProduct a wp:Protein}
 union
 {?geneProduct a wp:GeneProduct}
 
 ?geneProduct rdfs:label ?genraw .
	 bind (concat( ""^^xsd:string, ?genraw) as ?gen).
 filter(!regex(?gen,"[ a-z-]")). # ensures official gene names for subsequent neXtprot matching

 ?geneProduct dcterms:isPartOf ?pathway .
 ?pathway a wp:Pathway .
 ?pathway wp:organism ?organism .
 filter(contains(str(?organism),"9606")) # Human proteins
 ?pathway dc:title ?pathwayname .
 }
 ?entry a :Entry .
 ?entry :gene / :recommendedName / rdfs:label ?gen .
}
order by ?pathwayname'''
.
