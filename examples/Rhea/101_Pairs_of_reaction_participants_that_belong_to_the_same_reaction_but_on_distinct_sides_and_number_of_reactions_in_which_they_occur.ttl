@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix ex: <https://sparql.rhea-db.org/.well-known/sparql-examples/> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix schema: <https://schema.org/> .
@prefix sh: <http://www.w3.org/ns/shacl#> .

ex:101_Pairs_of_reaction_participants_that_belong_to_the_same_reaction_but_on_distinct_sides_and_number_of_reactions_in_which_they_occur
  a sh:SPARQLExecutable, sh:SPARQLSelectExecutable;
  rdfs:comment "Pairs of reaction participants that belong to the same reaction but on distinct sides and number of reactions in which they occur"@en;
  sh:select """PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rh:<http://rdf.rhea-db.org/>

SELECT
  ?chebi1
  ?compound1Name

  ?chebi2
  ?compound2Name

  (count(distinct ?reaction) as ?reactionCount)
WHERE {
  ?reaction rdfs:subClassOf rh:Reaction .
  ?reaction rh:status rh:Approved .

  ?reaction rh:side ?reactionSide1 .
  ?reactionSide1 rh:contains ?participant1 .
  ?participant1 rh:compound ?compound1 .
  ?compound1 rh:name ?compound1Name .
  ?compound1 rh:chebi ?chebi1 .

  ?reaction rh:side ?reactionSide2 .
  ?reactionSide2 rh:contains ?participant2 .
  ?participant2 rh:compound ?compound2 .
  ?compound2 rh:name ?compound2Name .
  ?compound2 rh:chebi ?chebi2 .


  ?reactionSide1 rh:transformableTo ?reactionSide2

  FILTER(?chebi1<?chebi2)
}
GROUP BY ?chebi1 ?chebi2 ?compound1Name ?compound2Name
ORDER BY DESC (?reactionCount)
LIMIT 100""";
  schema:target <https://sparql.rhea-db.org/sparql> .
