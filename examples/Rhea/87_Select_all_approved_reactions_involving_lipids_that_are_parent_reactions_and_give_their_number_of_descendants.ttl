@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix ex: <https://sparql.rhea-db.org/.well-known/sparql-examples/> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix schema: <https://schema.org/> .
@prefix sh: <http://www.w3.org/ns/shacl#> .

ex:87_Select_all_approved_reactions_involving_lipids_that_are_parent_reactions_and_give_their_number_of_descendants
  a sh:SPARQLExecutable, sh:SPARQLSelectExecutable;
  rdfs:comment "Select all approved reactions involving *lipids* (subclass of *CHEBI:18059*) that are parent reactions and give their number of descendants"@en ;
  sh:select """PREFIX CHEBI: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rh:<http://rdf.rhea-db.org/>

SELECT 
  DISTINCT 
    ?reaction 
    (COUNT(DISTINCT ?reactionDescendant) AS ?reactionDescendantCount)
    ?reactionEquation 
WHERE {
  ?reaction rdfs:subClassOf rh:Reaction .
  ?reaction rh:status rh:Approved .
  ?reaction rh:equation ?reactionEquation .
  ?reaction rh:side ?reactionSide .

  ?reactionSide rh:contains ?participant .
  ?participant rh:compound ?compound .
  ?compound rh:chebi ?chebi .
  ?chebi rdfs:subClassOf+ CHEBI:18059 .

  ?reactionDescendant rdfs:subClassOf rh:Reaction .
  ?reactionDescendant rh:status rh:Approved .

  ?reactionDescendant rdfs:subClassOf+ ?reaction .
}
group by ?reaction ?reactionEquation
ORDER BY ?reaction""";
  schema:target <https://sparql.rhea-db.org/sparql> .
